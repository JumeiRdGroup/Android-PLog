apply plugin: 'com.android.library'
apply plugin: 'bintray-release'

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION as int
    buildToolsVersion rootProject.ext.BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion rootProject.ext.MIN_SDK_VERSION as int
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION as int
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

/*
 * rules for including "lint.jar" in aar
 */
configurations {
    lintJarImport
}

dependencies {
    lintJarImport project(path: ":plog-lint", configuration: "lintJarOutput")
}

task copyLintJar(type: Copy) {
    from(configurations.lintJarImport) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

publish {
    artifactId = 'plog-lint'
    repoName = BINTRAY_REPO
    // userOrg =
    groupId = GROUP
    uploadName = POM_NAME
    publishVersion = VERSION_NAME
    desc = POM_DESCRIPTION
    website = POM_URL
    licences = POM_LICENCE_NAME
}